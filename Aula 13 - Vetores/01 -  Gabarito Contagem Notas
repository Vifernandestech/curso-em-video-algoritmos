Algoritmo "GabaritoContagemNotas"

//O objetivo do algoritmo é:

   //Cadastrar o gabarito de uma prova.
   //Registrar as respostas de vários alunos.
   //Comparar as respostas com o gabarito e calcular as notas.
   //Exibir as notas finais de cada aluno.
   //Exibir média da turma.
Var
   Contador, Contagem, Questao, C: Inteiro
   Gabarito: Vetor[1..5] de Caractere
   Nome: Vetor[1..3] de Caractere
   Nota: Vetor[1..5] de Caractere
   NotaFinal: Vetor[1..3] de Real
   SomaNotas, Media: Real

Inicio

//Cadastrar o gabarito de uma prova.
      Escreval("-------------------------------")
      Escreval("Passo 1 - Cadastro do Gabarito:")
      Escreval("-------------------------------")
      Para Contador <- 1 até 5 faça
           Escreval("Questão", Contador,":" )
           Leia(Gabarito[Contador])
           Enquanto (Gabarito[Contador] <> "A") E (Gabarito[Contador] <> "B") E (Gabarito[Contador] <> "C") E (Gabarito[Contador] <> "D") E (Gabarito[Contador] <> "E") E (Gabarito[Contador] <> "F") faça
               Escreval ("Valor inválido! Digite novamente. Apenas notas entre A e F.")
                   Escreval("Questão", Contador,":" )
                   Leia(Gabarito[Contador])
           FimEnquanto
      FimPara


//Registrar as respostas de vários alunos.
      Para Contagem <- 1 até 3 faça
           NotaFinal[Contagem] <- 0
           Escreval("-------------------------------")
           Escreval("ALUNO", Contagem,":")
           Escreval("-------------------------------")
           Escreval("Nome: ")
           Leia(Nome[Contagem])
           Escreval("RESPOSTAS DADAS:")
                               Para Questao <- 1 até 5 faça
                                    Escreval("Questão", Questao, ":")
                                    Leia(Nota[Questao])
                                        Enquanto (Nota[Questao] <> "A") E (Nota[Questao] <> "B") E (Nota[Questao] <> "C") E (Nota[Questao] <> "D") E (Nota[Questao] <> "E") E (Nota[Questao] <> "F") faça
                                             Escreval ("Valor inválido! Digite novamente. Apenas notas entre A e F.")
                                             Escreval("Questão", Questao,":" )
                                             Leia(Nota[Questao])
                                        FimEnquanto
                                        //Comparar as respostas com o gabarito e calcular as notas.
                                           Se Nota[Questao] = Gabarito[Questao] entao
                                            NotaFinal[Contagem] <- NotaFinal[Contagem] + 2.0
                                           FimSe

                               FimPara
      SomaNotas <- SomaNotas + NotaFinal[Contagem]
      FimPara
      //Exibir as notas finais de cada aluno.
      Escreval("-------------------------------")
      Escreval("NOTAS FINAIS:")
      Escreval("-------------------------------")
      Para C <- 1 até 3 faça
           Escreval("Aluno: ",Nome[C],"|", " Nota Final: ", NotaFinal[C])
           Escreval("-------------------------------")
      FimPara
      //Exibir média da turma.
      Media <- (SomaNotas/3)
      Escreval("A média da turma foi: ", Media:4:2)
      
      


           

      
      

Fimalgoritmo
